IDENTIFICATION DIVISION.
 PROGRAM-ID. SAMPLE5.
*>  THIS SAMPLE PROGRAM IS IN FREE FORMAT. THE PROGRAM MUST BE
*>  COMPILED WITH THE SRF COMPILER OPTION. THE SRF COMPILER OPTION
*>  SPECIFIES THE SOURCE FORMAT TYPE. SRF(FREE,FREE) TELLS THE
*>  COMPILER THAT THE SOURCE PROGRAM AND COPYBOOKS ARE IN FREE FORMAT.
ENVIRONMENT DIVISION.
 CONFIGURATION SECTION.
  SPECIAL-NAMES.
    SYSERR IS MESSAGE-DEVICE.
 INPUT-OUTPUT SECTION.
  FILE-CONTROL.
    SELECT MASTER-FILE
        ASSIGN       TO  INFILE
        ORGANIZATION IS  INDEXED
        ACCESS MODE  IS  SEQUENTIAL
        RECORD KEY   IS  GOODS-CODE OF MASTER-RECORD
        ALTERNATE RECORD KEY IS GOODS-NAME OF MASTER-RECORD.
    SELECT WORK-FILE
        ASSIGN       TO  WORK-FILE-NAME
        ORGANIZATION IS  LINE SEQUENTIAL.
*>  COMMENT LINES IN FREE FORMAT BEGIN WITH "*>".
*>  ONE OR MORE BLANKS CAN BE WRITTEN BEFORE THE "*>".
                     *>  THIS LINE IS AN EXAMPLE OF A COMMENT LINE.
DATA DIVISION.
 FILE SECTION.
 FD  MASTER-FILE.
 01  MASTER-RECORD.
     02  GOODS-RECORD.
         03  GOODS-CODE      PIC X(4).
         03  GOODS-NAME      PIC X(38).
         03  GOODS-PRICE     PIC 9(4)  BINARY.
 FD  WORK-FILE.
 01  WORK-RECORD.
     COPY "S_REC.CBL".
*>
 WORKING-STORAGE SECTION.
 01  WORK-FILE-NAME      PIC X(12) VALUE SPACE.
*>
 LINKAGE SECTION.
 01  PARAMETER.
     03  PARAMETER-LEN        PIC 9(4) BINARY.
     03  PARAMETER-STRING.
         05  PARAMETER-CHARACTER   PIC X
                 OCCURS 1 TO 8 DEPENDING ON PARAMETER-LEN.
*>
PROCEDURE DIVISION USING PARAMETER.
*>  (1) DETERMINE WORK-FILE NAME
     IF PARAMETER-LEN = 0
       DISPLAY "NOT A SPECIFIED PARAMETER."-
       "PLEASE SPECIFY PARAMETER."
          UPON MESSAGE-DEVICE
       GO TO TERM-PROC.
     IF PARAMETER-LEN > 8
       DISPLAY "PARAMETER LENGTH IS GREATER THAN 8-CHARACTERS"
          UPON MESSAGE-DEVICE
       GO TO TERM-PROC.
     MOVE PARAMETER-STRING(1:PARAMETER-LEN) TO WORK-FILE-NAME.
     ADD 1 TO PARAMETER-LEN.
     MOVE ".TMP" TO WORK-FILE-NAME(PARAMETER-LEN:4).
     DISPLAY "GENERATE WORK-FILE =" WORK-FILE-NAME
          UPON MESSAGE-DEVICE.
*>
*>  (2) OPEN THE INPUT FILE.
     OPEN INPUT  MASTER-FILE.
     START MASTER-FILE FIRST RECORD.
     OPEN OUTPUT WORK-FILE.
     MOVE SPACE TO WORK-RECORD.
*>
*>  (3) OUTPUT A LINE SEQUENTIAL FILE WHICH READS THE RECORDS FROM THE MASTER-FILE
*>      CLOSE THE FILE AFTER ALL RECORDS ARE READ
*>
 LOOP-POINT.
     READ MASTER-FILE AT END GO TO CLOSE-FILE-PROC.
     MOVE CORR MASTER-RECORD TO WORK-RECORD.
     WRITE WORK-RECORD.
     GO TO LOOP-POINT.
 CLOSE-FILE-PROC.
     CLOSE  MASTER-FILE  WORK-FILE.
*>
 PRINT-PROC.
*>  (4) PRINT THE WORK-FILE BY CALLING SUB ROUTINE
     CALL "PRINTPRC" USING WORK-FILE-NAME.
*>
 TERM-PROC.
    EXIT PROGRAM.
END PROGRAM SAMPLE5.
