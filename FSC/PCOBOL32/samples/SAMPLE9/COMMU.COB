000100 IDENTIFICATION DIVISION.
000200  PROGRAM-ID. COMMU.
000300*
000400 DATA DIVISION.
000500  WORKING-STORAGE SECTION.
000600      COPY "PRM_REC.CBL".
000700*
000800  01  MSG-AREA.
000900      02  COMM-AREA  OCCURS 1 TO 32000 DEPENDING ON MSG-LEN-W.
001000          03              PIC X(1).
001100  01  MSG-LEN-W           PIC 9(5).
001200  01  TRANSMIT-MSG1       PIC X(30) VALUE "MESSAGE SENT FROM COMMU".
001200  01  TRANSMIT-MSG2       PIC X(25) VALUE "MESSAGE SENT : PRIORITY=3".
001300*
001400 PROCEDURE DIVISION.
001500** CONNECT TO THE SERVER.
001600      MOVE "SERVER1" TO SERVER-NAME.
001700      CALL "COBCI_OPEN" WITH C LINKAGE
001800           USING BY REFERENCE STATUS-AREA
001900                 BY REFERENCE SERVER-NAME
002000                 BY REFERENCE SERVER-ID
002100                 BY VALUE     COMM-RESERVED.
002200      IF PROGRAM-STATUS = 0
002300           DISPLAY   "CONNECTION TO THE SERVER SUCCEEDED."
002400           DISPLAY   "  SERVER-NAME      : " SERVER-NAME
002500      ELSE
002600           DISPLAY   "CONNECTION TO THE SERVER FAILED."
002700           DISPLAY   "  SERVER-NAME      : " SERVER-NAME
002800           DISPLAY   "  ERR-CODE         : " ERR-CODE    OF STATUS-AREA
002900           DISPLAY   "  DETAIL-CODE      : " DETAIL-CODE OF STATUS-AREA
003000           GO TO EXIT-PROC.
003100*
003200** THE MESSAGE READ FROM LOGICAL-DESTINATION ON SERVER
003300      MOVE "MYLD1" TO LOGICAL-DESTINATION.
003400      MOVE 32000   TO BUFFER-LEN OF RECIVE-PARAM.
003500      MOVE 0       TO PROC-KIND  OF RECIVE-PARAM.
003600      CALL "COBCI_READ" WITH C LINKAGE
003700           USING BY REFERENCE STATUS-AREA
003800                 BY VALUE     SERVER-ID
003900                 BY REFERENCE LOGICAL-DESTINATION
004000                 BY REFERENCE RECIVE-PARAM
004100                 BY REFERENCE MSG-AREA
004200                 BY VALUE     COMM-RESERVED.
004300      IF PROGRAM-STATUS = 0
004400           DISPLAY   "A MESSAGE HAS BEEN RECEIVED."
004500           DISPLAY   "  DESTINATION      : " LOGICAL-DESTINATION
004600           MOVE MSG-LEN OF RECIVE-PARAM TO MSG-LEN-W
004700           DISPLAY   "  MESSAGE RECEIVED : " MSG-AREA
004800      ELSE
004900           DISPLAY   "A  MESSAGE FAILED TO BE RECEIVED."
005000           DISPLAY   "  DESTINATION      : " LOGICAL-DESTINATION
005100           DISPLAY   "  ERR-CODE         : " ERR-CODE    OF STATUS-AREA
005200           DISPLAY   "  DETAIL-CODE      : " DETAIL-CODE OF STATUS-AREA.
005300*
005400** THE MESSAGE WRITE LOGICAL-DESTINATION ON SERVER
005500      MOVE 25            TO MSG-LEN-W.
005600      MOVE TRANSMIT-MSG2 TO MSG-AREA.
005700      MOVE "MYLD1"       TO LOGICAL-DESTINATION.
005800      MOVE MSG-LEN-W     TO MSG-LEN   OF TRANSMIT-PARAM.
005900      MOVE 3             TO PRIORITY  OF TRANSMIT-PARAM.
006000      MOVE 0             TO PROC-KIND OF TRANSMIT-PARAM.
006100      CALL "COBCI_WRITE" WITH C LINKAGE
006200           USING BY REFERENCE STATUS-AREA
006300                 BY VALUE     SERVER-ID
006400                 BY REFERENCE LOGICAL-DESTINATION
006500                 BY REFERENCE TRANSMIT-PARAM
006600                 BY REFERENCE MSG-AREA
006700                 BY VALUE     COMM-RESERVED.
006800      IF PROGRAM-STATUS = 0
006900           DISPLAY   "A MESSAGE HAS BEEN WRITTEN."
007000           DISPLAY   "  DESTINATION      : " LOGICAL-DESTINATION
007100      ELSE
007200           DISPLAY   "A  MESSAGE FAILED TO BE WRITTEN."
007300           DISPLAY   "  DESTINATION      : " LOGICAL-DESTINATION
007400           DISPLAY   "  ERR-CODE         : " ERR-CODE    OF STATUS-AREA
007500           DISPLAY   "  DETAIL-CODE      : " DETAIL-CODE OF STATUS-AREA.
007600*
007700      MOVE 30            TO MSG-LEN-W.
007800      MOVE TRANSMIT-MSG1 TO MSG-AREA.
007900      MOVE "MYLD2"       TO LOGICAL-DESTINATION.
008000      MOVE MSG-LEN-W     TO MSG-LEN   OF TRANSMIT-PARAM.
008100      MOVE 2             TO PRIORITY  OF TRANSMIT-PARAM.
008200      MOVE 0             TO PROC-KIND OF TRANSMIT-PARAM.
008300      CALL "COBCI_WRITE" WITH C LINKAGE
008400           USING BY REFERENCE STATUS-AREA
008500                 BY VALUE     SERVER-ID
008600                 BY REFERENCE LOGICAL-DESTINATION
008700                 BY REFERENCE TRANSMIT-PARAM
008800                 BY REFERENCE MSG-AREA
008900                 BY VALUE     COMM-RESERVED.
009000      IF PROGRAM-STATUS = 0
009100           DISPLAY   "A MESSAGE HAS BEEN WRITTEN."
009200           DISPLAY   "  DESTINATION      : " LOGICAL-DESTINATION
009300      ELSE
009400           DISPLAY   "A  MESSAGE FAILED TO BE WRITTEN."
009500           DISPLAY   "  DESTINATION      : " LOGICAL-DESTINATION
009600           DISPLAY   "  ERR-CODE         : " ERR-CODE    OF STATUS-AREA
009700           DISPLAY   "  DETAIL-CODE      : " DETAIL-CODE OF STATUS-AREA.
009800*
009900** DISCONNECT WITH SERVER
010000      CALL "COBCI_CLOSE" WITH C LINKAGE
010100           USING BY REFERENCE STATUS-AREA
010200                 BY VALUE     SERVER-ID
010300                 BY VALUE     COMM-RESERVED.
010400      IF PROGRAM-STATUS = 0
010500           DISPLAY   "DISCONNECTION FROM THE SERVER SUCCEEDED."
010600           DISPLAY   "  SERVER-NAME      : " SERVER-NAME
010700      ELSE
010800           DISPLAY   "DISCONNECTION FROM THE SERVER FAILED."
010900           DISPLAY   "  SERVER-NAME      : " SERVER-NAME
011000           DISPLAY   "  ERR-CODE         : " ERR-CODE    OF STATUS-AREA
011100           DISPLAY   "  DETAIL-CODE      : " DETAIL-CODE OF STATUS-AREA.
011200*
011300  EXIT-PROC.
011400     EXIT PROGRAM.
011500 END PROGRAM COMMU.
